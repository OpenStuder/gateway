grammar ::= request | response

request ::= authorize_request | enumerate_request | read_property_request | write_property_request | subscribe_property_request

authorize_request ::= "a" sp authorize_user sp authorize_password (sp authorize_protocol_version)?
authorize_user ::= string
authorize_password ::= string
authorize_protocol_version ::= integer

enumerate_request ::= "enum"

read_property_request ::= "rdpr" sp property_id

write_property_request ::= "wrpr" sp property_id (sp property_value)?

subscribe_property_request ::= "subp" sp property_id


response ::= error_response | authorize_response | enumerate_response | read_property_response | write_property_response | subscribe_property_response | property_update_indication | message_indication

error_response ::= "ERRO" sp error_reason
error_reason ::= string

authorize_response ::= "A" sp status sp authorize_protocol_version

enumerate_response ::= "ENUM" sp status sp enumerate_device_count
enumerate_device_count ::= integer

read_property_response ::= "RDPR" sp property_id sp status sp property_value

write_property_response ::= "WRPR" sp property_id sp status

subscribe_property_response ::= "SUBP" sp property_id sp status

property_update_indication ::= "PRUP" sp property_id sp property_value

message_indication ::= "DMSG" sp device_id sp message_id (sp message_text)?
message_id ::= integer
message_text ::= (string | sp)+


status ::= integer
property_value ::= (string | ".")*

describe_id ::= (property_id | device_id | access_id)
access_id ::= device_access_selector
device_id ::= device_access_selector "." device_selector
property_id ::= device_id "." property_selector

device_access_selector ::= (digit | char)+
device_selector ::= (digit | char)+
property_selector ::= digit+

json ::= "{" ([.] | nl)* "}"
string ::= (char | digit | [_-])*
char ::= lower_char | upper_char
lower_char ::= [a-z]
upper_char ::= [A-Z]
integer ::= "-"? digit+
digit ::= [0-9]
nl ::= "\n"
sp ::= " "
